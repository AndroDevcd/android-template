static int getVersionCode() {
    if (System.getenv("CI") != 'true') return 1

    def lines = ["sh", "-c", "git rev-list --all --count"].execute().text
            .trim()
            .readLines()

    if (lines.isEmpty()) {
        return 1
    } else {
        return lines.first().toInteger()
    }
}

static String getVersionName() {
    if (System.getenv("CI") != 'true') return "1.0.0"

    def lines = ["sh", "-c", "git describe --tags"].execute().text
            .trim()
            .readLines()

    if (lines.isEmpty()) {
        return "1.0.0"
    } else {
        return lines.first()
    }
}

ext.buildConfig = [
        'minSdk'        : 22,
        'targetSdk'     : 28,
        'compileSdk'    : 28,
        'buildTools'    : "27.0.3",
        'code'          : getVersionCode(),
        'name'          : getVersionName()
]

ext.versions = [
        'kotlin'            : '1.2.51',
        'supportLibrary'    : '28.0.0-alpha3',
        'arch'              : '1.1.1',
        'archRoom'          : '1.0.0',
        'atsl'              : '1.0.1',
        'espresso'          : '3.0.1',
        'dagger'            : '2.16',
        'okhttp'            : '3.10.0',
        'retrofit'          : '2.4.0',
        'rxBinding'         : '2.1.1',
        'kotshi'            : '1.0.2',
        'ktlint'            : '0.24.0'
]

ext.libs = [
        // Support Libraries
        'android': [
                'arch':[
                        'db': "android.arch.persistence:db:${versions.archRoom}",
                        'room': [
                                'runtime': "android.arch.persistence.room:runtime:${versions.archRoom}",
                                'compiler': "android.arch.persistence.room:compiler:${versions.archRoom}",
                                'test': "android.arch.persistence.room:testing:${versions.archRoom}",
                                'adapterRxJava2': "android.arch.persistence.room:rxjava2:${versions.archRoom}"
                        ],
                        'viewmodel': "android.arch.lifecycle:viewmodel:${versions.arch}"
                ],
                'support': [
                        'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
                        'appcompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
                        'cardview': "com.android.support:cardview-v7:${versions.supportLibrary}",
                        'compat': "com.android.support:support-compat:${versions.supportLibrary}",
                        'coreUi': "com.android.support:support-core-ui:${versions.supportLibrary}",
                        'coreUtils': "com.android.support:support-core-utils:${versions.supportLibrary}",
                        'design': "com.android.support:design:${versions.supportLibrary}",
                        'fragment': "com.android.support:support-fragment:${versions.supportLibrary}",
                        'recyclerView': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                        'constraintLayout':'com.android.support.constraint:constraint-layout:1.1.2'
                ],
                // Android Test
                'test': [
                        'espresso': [
                                'core': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
                                'contrib': "com.android.support.test.espresso:espresso-contrib:${versions.espresso}"
                        ],
                        'runner': "com.android.support.test:runner:${versions.atsl}",
                        'rules': "com.android.support.test:rules:${versions.atsl}"
                ],
                'x': [
                        'coreKtx': 'androidx.core:core-ktx:0.3'
                ]
        ],

        // Libraries
        'dagger': [
                'runtime': "com.google.dagger:dagger:${versions.dagger}",
                'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
                'android': [
                        'runtime': "com.google.dagger:dagger-android:${versions.dagger}",
                        'support': "com.google.dagger:dagger-android-support:${versions.dagger}",
                        'processor': "com.google.dagger:dagger-android-processor:${versions.dagger}"
                ]
        ],

        'javaxInject': "javax.inject:javax.inject:1",    // Used for Java modules only

        'rx': [
                'java': "io.reactivex.rxjava2:rxjava:2.1.16",
                'kotlin': "io.reactivex.rxjava2:rxkotlin:2.2.0",
                'android': "io.reactivex.rxjava2:rxandroid:2.0.2",
                'binding': [
                        'kotlin': "com.jakewharton.rxbinding2:rxbinding-kotlin:${versions.rxBinding}",
                        'appcompat': "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:${versions.rxBinding}",
                        'design': "com.jakewharton.rxbinding2:rxbinding-design-kotlin:${versions.rxBinding}",
                        'recyclerView': "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:${versions.rxBinding}",
                ],
                'permissions': "com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar",
                'relay': "com.jakewharton.rxrelay2:rxrelay:2.0.0"
        ],
        'okhttp': [
                'client': "com.squareup.okhttp3:okhttp:${versions.okhttp}",
                'logging': "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"
        ],
        'retrofit': [
                'client': "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                'adapterRxJava2': "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
                'converterGson': "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
                'converterMoshi': "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"
        ],
        'moshi': "com.squareup.moshi:moshi:1.5.0",
        'kotshi': [
                'api': "se.ansman.kotshi:api:${versions.kotshi}",
                'compiler': "se.ansman.kotshi:compiler:${versions.kotshi}"
        ],
        'kotlin': [
                'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jre7:${versions.kotlin}",
                'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
                'test': [
                        'runtime': "org.jetbrains.kotlin:kotlin-test:${versions.kotlin}",
                        'junit': "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}"
                ]
        ],
        'timber': "com.jakewharton.timber:timber:4.7.1",
        'autodispose': "com.uber.autodispose:autodispose-android-archcomponents-kotlin:0.6.1",
        // Test
        'junit': "junit:junit:4.12",
        'assertj': "org.assertj:assertj-core:3.8.0",
        'dexmaker': "com.linkedin.dexmaker:dexmaker-mockito:2.2.0"
]